# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("systemres.gni")

dep_list = []

foreach(font, fonts_list) {
  font_name = font.font_name
  ohos_prebuilt_etc(font_name) {
    source = font.font_path
    module_install_dir = "fonts"
    subsystem_name = "global"
    part_name = "ace_engine"
  }
  dep_list += [ font_name ]
}

group("ohos_fonts") {
  deps = []
  foreach(dep, dep_list) {
    deps += [ ":${dep}" ]
  }

  if (system_resources_support_ext) {
    deps += [ "${systemres_ext_path}:ohos_fonts_ext" ]
  }
}

ohos_copy("copy_preview_fonts") {
  sources = []
  foreach(font, fonts_list) {
    sources += [ font.font_path ]
  }
  outputs =
      [ target_out_dir + "/previewer/common/bin/fonts/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/previewer/common/bin/"
  module_install_name = ""
  subsystem_name = "global"
  part_name = "system_resources"
}
